#version 430
#extension GL_ARB_gpu_shader_int64 : enable

struct Particle {
    vec3 Position;
    vec3 PredictedPosition;
    vec3 Velocity;
    vec3 PressureAcceleration;

    float density;
    float pressure;

    uint Gridhash;
};

layout(std430, binding = 0) buffer ParticleBuffer {
    Particle particles[];
};

layout(std430, binding = 1) buffer HashLookupTable {
    int hashLookupTable[];
};

layout(local_size_x = 704) in;

layout(location = 0) uniform float gridSize;
layout(location = 1) uniform int hashfunctionX;
layout(location = 2) uniform int hashfunctionY;
layout(location = 3) uniform int hashfunctionZ;
layout(location = 4) uniform float mass;
layout(location = 5) uniform float smoothingKernelDerivativeMultiplier;
layout(location = 6) uniform float viscosityMultiplier;

uint calcHash(ivec3 gridpos) {
    return (uint((gridpos.x * hashfunctionX) ^ (gridpos.y * hashfunctionY) ^ (gridpos.z * hashfunctionZ)) % particles.length());
}

uint spreadBits(uint v) {
    v = (v | (v << 16)) & 0x030000FF;
    v = (v | (v << 8)) & 0x0300F00F;
    v = (v | (v << 4)) & 0x030C30C3;
    v = (v | (v << 2)) & 0x09249249;
    return v;
}

uint zOrder(vec3 v) {
    uint x = uint(v.x) % 128;
    uint y = uint(v.y) % 128;
    uint z = uint(v.z) % 128;
    return spreadBits(x) | (spreadBits(y) << 1) | (spreadBits(z) << 2);
}

float smoothingKernelDerivative(float h, float r) {
    return smoothingKernelDerivativeMultiplier * ((r - h)/10) * step(r, h);
}


void main() {
    uint index = gl_GlobalInvocationID.x;
    if (index >= particles.length()) return;

    Particle p = particles[index];
    ivec3 gridpos = ivec3(p.PredictedPosition / gridSize);

    vec3 pressureForce = vec3(0.0);

    // Precompute the hashes for the neighboring cells
    uint neighborHashes[27];
    int count = 0;
    for (int ix = -1; ix <= 1; ix++) {
        for (int iy = -1; iy <= 1; iy++) {
            for (int iz = -1; iz <= 1; iz++) {
                ivec3 neighborGridPos = gridpos + ivec3(ix, iy, iz);
                //neighborHashes[count++] = calcHash(neighborGridPos);
                neighborHashes[count++] = zOrder(neighborGridPos)% hashLookupTable.length();;

            }
        }
    }


    // Loop through precomputed hashes and particles
    for (int i = 0; i < 27; i++) {
        uint key = neighborHashes[i];
        int startIndex = hashLookupTable[key];
        float mask = 1;
        for (int j = startIndex; j < particles.length() && mask != 0; j++) {
            Particle neighbor = particles[j];
            mask = float(key == neighbor.Gridhash);

            vec3 offsetVec = neighbor.PredictedPosition - p.PredictedPosition;
            float distance = length(offsetVec);

            if (distance > gridSize || index == j) continue;

            vec3 dir = distance == 0.0 ? normalize(neighbor.Position - particles[j-1].Position) : offsetVec / distance;

            float kernelDerivative = smoothingKernelDerivative(gridSize, distance);
            float neighborDensity = neighbor.density;
            float sharedPressure = (neighbor.pressure + p.pressure) / 2.0;
            pressureForce += dir * (sharedPressure * kernelDerivative * mass / neighborDensity) * mask;

            // Add viscosity
            vec3 velocityDiff = neighbor.Velocity - p.Velocity;
            pressureForce += velocityDiff * viscosityMultiplier * (-kernelDerivative / (neighborDensity * 10)) * mask;
        }
    }

    particles[index].PressureAcceleration = pressureForce / mass;
}
